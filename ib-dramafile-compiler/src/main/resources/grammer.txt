# 定义符号到 Token code 的映射，便于推导式的阅读
{ = BLOCK_LEFT
} = BLOCK_RIGHT
( = BRACKET_LEFT
) = BRACKET_RIGHT
= = ASSIGN
+ = PLUS
- = MINUS
* = TIMES
/ = DIVISION
; = STMD_END
. = PROP
, = COMMA

# 定义推导式
# 文法开始符号必须为 CFG
CFG -> Imports Identity DramaName { Methods }

# 导入语句列表
Imports -> IMPORT Identity Alias ; Imports
Imports -> ϵ

# 重定义导入角色名
Alias -> IDENTIFIER
Alias -> ϵ

# dramaName 的声明
DramaName -> ( STR )
DramaName -> ϵ

# 方法列表
Methods -> Method Methods
Methods -> ϵ

# 方法
Method -> Type IDENTIFIER ( ParamDeclares ) { Stmds }

# 参数声明列表
ParamDeclares -> FirstParamDeclare NonFirstParamDeclare
ParamDeclares -> ϵ

# 第一个参数声明
FirstParamDeclare -> Type IDENTIFIER

# 非第一个参数声明
NonFirstParamDeclare -> , FirstParamDeclare NonFirstParamDeclare
NonFirstParamDeclare -> ϵ 

# 变量类型，暂时只用一个 void 占位
Type -> TYPE_VOID

# 参数列表
Params -> FirstParam NonFirstParams
Params -> ϵ

# 第一个参数
FirstParam -> Expr

# 非第一个参数
NonFirstParams -> , FirstParam NonFirstParams
NonFirstParams -> ϵ

# 语句列表
Stmds -> Stmd Stmds
Stmds -> ϵ

# 语句，暂时只支持部分语法
Stmd -> Expr ;
Stmd -> SLEEP ( INT ) ;
# Stmd -> { Stmds }
# Stmd -> if ( Expr ) { Stmds }
# Stmd -> while ( Expr ) { Stmds }

# 加减、赋值优先级表达式，暂时只支持部分语法
# Expr -> Expr + Term
# Expr -> Expr - Term
Expr -> Term
# Expr -> IDENTIFIER = Expr
Expr -> ADD IDENTIFIER ( Params )
Expr -> IMG ( STR )
Expr -> AUDIO ( STR , STR )

# 乘除优先级表达式，暂时只支持部分语法
# Term -> Term * Factor
# Term -> Term / Factor
Term -> Factor

# 常量、变量、括号优先级表达式，暂时只支持部分语法
# Factor -> Identity
# Factor -> ( Expr )
Factor -> Const

# 变量
Identity -> Identity . IDENTIFIER
Identity -> IDENTIFIER

# 常量，暂时只有整数和单精度浮点数
Const -> INT
Const -> FLOAT
Const -> STR